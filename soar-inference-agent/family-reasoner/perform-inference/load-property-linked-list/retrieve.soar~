sp {load-property-linked-list*propose*retrieve*success
   (state <s> ^name load-property-linked-list
              ^smem.result.retrieved <retrieved>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name retrieve-success
         ^retrieved <retrieved>)
}

sp {load-property-linked-list*apply*retrieve*success 
   (state <s> ^name load-property-linked-list 
              ^operator <op>
              ^superstate <ss>)
   (<op> ^name retrieve-success
         ^retrieved <retrieved>)
-->
   (<ss> ^property-linked-list <retrieved>) 
   (write |DEBUG> Retrieved property-linked-list: | <retrieved> (crlf))
}

sp {load-property-linked-list*propose*retrieve*failure
   (state <s> ^name load-property-linked-list
              ^smem.result.failure)
-->
   (<s> ^operator <op> + =)
   (<op> ^name retrieve-failure)
}

sp {load-property-linked-list*apply*retrieve*failure
   (state <s> ^name load-property-linked-list
              ^operator <op>
              ^superstate.superstate.inference <inference>)
   (<op> ^name retrieve-failure)
-->
   (<inference> ^result |Failure: Cannot find property-linked-list|)
   (write |DEBUG> Failed to retrieve property-linked-list| (crlf))
}

# Clean up
sp {load-property-linked-list*apply*retrieve*clean-up*success
   (state <s> ^name load-property-linked-list
              ^operator <op>
              ^smem.command <command>)
   (<op> ^name retrieve-success)
   (<command> ^query <query>)
-->
   (<command> ^query <query> -)
   (write |DEBUG> Cleaned up query for property-linked-list (success) | (crlf))
}

sp {load-property-linked-list*apply*retrieve*clean-up*failure
   (state <s> ^name load-property-linked-list
              ^operator <op>
              ^smem.command <command>)
   (<op> ^name retrieve-failure)
   (<command> ^query <query>)
-->
   (<command> ^query <query> -)
   (write |DEBUG> Cleaned up query for property-linked-list (failure) | (crlf))
}

